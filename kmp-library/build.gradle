plugins {
    id 'java'
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id "org.jlleitschuh.gradle.ktlint" version '10.2.1'
    id "org.jetbrains.kotlin.jvm" version "2.2.0"
    id 'signing'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
    withJavadocJar()
    withSourcesJar()
}

components.java.withVariantsFromConfiguration(configurations.testFixturesApiElements) { skip() }
components.java.withVariantsFromConfiguration(configurations.testFixturesRuntimeElements) { skip() }

test {
    useJUnitPlatform()
}

dependencies {
    api('com.guardsquare:proguard-core:9.1.8')

    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:2.2.0"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:2.2.0"
    testImplementation group: "dev.zacsweers.kctfork", name: "core", version: "0.8.0"
    testImplementation 'io.kotest:kotest-runner-junit5-jvm:5.7.2' // for kotest framework
    testImplementation 'io.kotest:kotest-runner-junit5-jvm:5.7.2' // for kotest framework
    testImplementation 'io.kotest:kotest-assertions-core-jvm:5.7.2' // for kotest core jvm assertions
    testImplementation 'io.kotest:kotest-property-jvm:5.7.2' // for kotest property test
    testImplementation 'io.mockk:mockk:1.13.12' // for mocking

    testImplementation(testFixtures("com.guardsquare:proguard-core:9.1.8"))
}

publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            groupId = 'com.guardsquare'
            artifactId = 'kotlin-metadata-printer'
            version = version
            pom {
                name = "Kotlin Metadata Printer"
                description = "A tool for printing Kotlin metadata attached to Java class files in readable form, based on ProGuardCORE."
                url = "https://github.com/Guardsquare/kotlin-metadata-printer"
                licenses {
                    license {
                        name = 'Apache License Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                issueManagement {
                    system = 'Github Tracker'
                    url = 'https://github.com/Guardsquare/kotlin-metadata-printer/issues'
                }
                scm {
                    url = 'https://github.com/Guardsquare/kotlin-metadata-printer.git'
                    connection = 'scm:git:https://github.com/Guardsquare/kotlin-metadata-printer.git'
                }
                developers {
                    developer {
                        id = 'james.hamilton'
                        name = 'James Hamilton'
                        organization = 'Guardsquare'
                        organizationUrl = 'https://www.guardsquare.com/'
                        roles = ['Project Administrator', 'Developer']
                    }
                }
            }

            from components.java

            if (project.hasProperty('PROGUARD_SIGNING_KEY')) {
                // We use in-memory ascii-armored keys
                // See https://docs.gradle.org/current/userguide/signing_plugin.html#sec:in-memory-keys
                signing {
                    String key = project.findProperty('PROGUARD_SIGNING_KEY')
                    String password = project.findProperty('PROGUARD_SIGNING_PASSWORD')
                    useInMemoryPgpKeys(key, password)
                    sign publication
                }
            }
        }
    }
}

