import proguard.gradle.ProGuardTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(libs.proguard.gradle)
    }
}

plugins {
    id 'java'
    id 'application'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

application {
    mainClass = "com.guardsquare.proguard.kotlin.printer.KotlinMetadataPrinterCli"
}

jar {
    dependsOn(":kmp-library:jar")
    manifest {
        attributes 'Main-Class': application.mainClass
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    archiveFileName = "kotlin-metadata-printer.jar"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    implementation project(path: ":kmp-library")
    implementation(libs.proguard.core)
    implementation(libs.proguard.core.android)

    // For some string utils.
    implementation(libs.apache.commons.lang)

    // For building the JSON output.
    implementation(libs.json)

    // picocli for command line parsing.
    implementation(libs.picocli)
}

def compiler = javaToolchains.compilerFor {
    languageVersion = JavaLanguageVersion.of(8)
}

def copyBuild = tasks.register("copyBuild" ,Copy) {
    tasks.assemble.dependsOn(it)

    from(tasks.jar)
    into file("$rootDir/lib")
}

// TODO: proguard shrinking temporary disabled, since log4j in ProGuardCORE causes problems
def proguard = tasks.register("proguard", ProGuardTask) {
    //tasks.assemble.dependsOn(it)

    injars tasks.jar, filter: '**.class,log4j2.xml,META-INF/services/**,META-INF/MANIFEST.MF'
    def compileFiles = configurations.compileClasspath.incoming.artifacts.artifactFiles
    def runtimeFiles = configurations.runtimeClasspath.incoming.artifacts.artifactFiles

    libraryjars(compileFiles-runtimeFiles)
    outjars(project.layout.buildDirectory.file("libs/kotlin-metadata-printer-shrunk.jar"))

    libraryjars "${compiler.get().metadata.installationPath}/jre/lib/rt.jar"

    dontwarn "picocli.**"
    dontwarn "org.xml.sax.**"
    dontwarn "org.objectweb.asm.**"

    forceprocessing
    ignorewarnings
    verbose
    keep 'public class  com.guardsquare.proguard.kotlin.printer.KotlinMetadataPrinterCli {' +
            'public static void main(java.lang.String[]);' +
            '}'

    optimizationpasses 3
    allowaccessmodification
    keepattributes "*Annotation*"

    adaptresourcefilecontents "META-INF/MANIFEST.MF,**.properties,META-INF/services/**"

    keep allowshrinking: true, 'enum picocli.** { *; }'

    keepclassmembers 'class * extends java.lang.Enum {' +
            'public static **[] values();' +
            'public static ** valueOf(java.lang.String);' +
            '}'

    keepclassmembers 'class * {'+
            '@picocli.CommandLine$* *;' +
            '}'

    repackageclasses 'o'
    keep 'class kotlinx.metadata.jvm.impl.JvmMetadataExtensions { *; }'
    keep 'class kotlinx.metadata.impl.extensions.MetadataExtensions { *; }'
    keep 'class proguard.classfile.kotlin.visitor.AllKotlinAnnotationArgumentVisitor {*;}'
    keep includecode: true, 'class org.apache.logging.log4j.** { *; }'
}

clean {
    delete file("$rootDir/lib/kotlin-metadata-printer.jar")
}

tasks.withType(Tar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(Zip).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
